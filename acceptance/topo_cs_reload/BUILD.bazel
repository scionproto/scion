load("//tools/lint:go.bzl", "go_test")
load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball")

go_test(
    name = "go_default_test",
    srcs = ["reload_test.go"],
    args = [
        "-scion_pki",
        "$(location //scion-pki/cmd/scion-pki:scion-pki)",
        "-topo",
        "$(location testdata/test.topo)",
    ],
    data = [
        "docker-compose.yml",
        "testdata/topology_reload.json",
        ":control.tar",
        ":invalid_changed_ip",
        ":invalid_changed_port",
        ":testdata/gen_crypto.sh",
        ":testdata/test.topo",
        "//acceptance/topo_common:invalid_reloads",
        "//acceptance/topo_common:topology",
        "//scion-pki/cmd/scion-pki",
    ],
    tags = [
        "exclusive",
        "integration",
    ],
    deps = [
        "//private/topology:go_default_library",
        "@com_github_stretchr_testify//assert:go_default_library",
        "@com_github_stretchr_testify//require:go_default_library",
        "@io_bazel_rules_go//go/tools/bazel:go_default_library",
    ],
)

# control container
oci_tarball(
    name = "control.tar",
    format = "docker",
    image = ":control_image",
    repo_tags = ["scion/" + package_name() + ":control"],
)

oci_image(
    name = "control_image",
    base = "//docker:control",
    cmd = [
        "--config",
        "/cs.toml",
    ],
    entrypoint = ["/app/control"],
    tars = [
        ":control_data",
    ],
)

pkg_tar(
    name = "control_data",
    srcs = [
        "testdata/cs.toml",
        "testdata/tls.key",
        "testdata/tls.pem",
        "testdata/topology_reload.json",
        ":invalid_changed_ip",
        ":invalid_changed_port",
        "//acceptance/topo_common:invalid_reloads",
        "//acceptance/topo_common:topology",
    ],
)

genrule(
    name = "invalid_changed_ip",
    srcs = ["//acceptance/topo_common:topology"],
    outs = ["topology_invalid_changed_ip.json"],
    cmd = "jq '.control_service[].addr = \"242.42.42.2:31002\"' $(location //acceptance/topo_common:topology) > $@",
)

genrule(
    name = "invalid_changed_port",
    srcs = ["//acceptance/topo_common:topology"],
    outs = ["topology_invalid_changed_port.json"],
    cmd = "jq '.control_service[].addr = \"127.0.0.12:42424\"' $(location //acceptance/topo_common:topology) > $@",
)
