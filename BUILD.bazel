load("@bazel_skylib//rules:common_settings.bzl", "bool_flag", "string_flag")
load("@bazel_gazelle//:def.bzl", "gazelle")
load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("@io_bazel_rules_go//go:def.bzl", "nogo")
load("//tools/lint:go_config.bzl", "go_lint_config")
load("//tools/lint:write_source_files.bzl", "write_source_files")
load("//tools/lint/python:flake8_config.bzl", "flake8_lint_config")
load("//:nogo.bzl", "nogo_deps")
load("@com_github_bazelbuild_buildtools//buildifier:def.bzl", "buildifier")
load("@npm//private/mgmtapi/tools:@stoplight/spectral-cli/package_json.bzl", spectral_bin = "bin")

# gazelle:prefix github.com/scionproto/scion
# gazelle:map_kind go_library go_library //tools/lint:go.bzl
# gazelle:map_kind go_test go_test //tools/lint:go.bzl
# gazelle:exclude docker/_build/**
# gazelle:exclude proto/**
# gazelle:exclude doc/**
# gazelle:exclude rules_openapi/tools/node_modules/**
# gazelle:exclude tools/lint/**/testdata/src/**

# We suport two sqlite implementations: modernc and mattn. Each implementation
# has a corresponding sqlite_<impl>.go "driver" in private/storage/db. Which
# driver gets compiled and linked is controled by a go build tag: sqlite_mattn
# or sqlite_modernc. Those are specified on the command line
# with "--define gotags=sqlite_mattn" or "--define gotags=sqlite_modernc"
# (see the build options in .bazelrc).
#
# Unfortunately Gazelle needs to be given these tags explicitly via the builtags
# attribute. So, to ensure consistency we have to translate our two gotags into
# build_tags. To that end, we create two config_setting flags that are
# set in response to matching the gotags value and use them to select the relevant
# tag for gazelle. (The "define_value" attribute of config_setting doesn't define
# anything. It matches a key-value pair from "--define").
#
# This is simplistic but the complete, by-the-everchanging-bazel-book, solution
# is ludicrously complicated. Go there if and when needed.
config_setting(
    name = "sqlite_mattn_netgo",
    define_values = {
        "gotags": "sqlite_mattn,netgo",
    },
)

config_setting(
    name = "sqlite_modernc_netgo",
    define_values = {
        "gotags": "sqlite_modernc,netgo",
    },
)

config_setting(
    name = "sqlite_mattn",
    define_values = {
        "gotags": "sqlite_mattn",
    },
)

config_setting(
    name = "sqlite_modernc",
    define_values = {
        "gotags": "sqlite_modernc",
    },
)

gazelle(
    name = "gazelle",
    build_tags = select({
        ":sqlite_modernc_netgo": [
            "sqlite_modernc",
            "sqlite_modernc_netgo",
        ],
        ":sqlite_modernc": [
            "sqlite_modernc",
        ],
        ":sqlite_mattn_netgo": [
            "sqlite_mattn",
            "netgo",
        ],
        ":sqlite_mattn": [
            "sqlite_mattn",
        ],
    }),
    command = "update",
    extra_args = [
        "-mode",
        "fix",
        "-go_naming_convention",
        "go_default_library",
    ],
)

gazelle(
    name = "gazelle_diff",
    build_tags = select({
        ":sqlite_modernc_netgo": [
            "sqlite_modernc",
            "netgo",
        ],
        ":sqlite_modernc": [
            "sqlite_modernc",
        ],
        ":sqlite_mattn_netgo": [
            "sqlite_mattn",
            "netgo",
        ],
        ":sqlite_mattn": [
            "sqlite_mattn",
        ],
    }),
    command = "update",
    extra_args = [
        "-mode",
        "diff",
        "-go_naming_convention",
        "go_default_library",
    ],
)

gazelle(
    name = "gazelle_update_repos",
    command = "update-repos",
    extra_args = [
        "-prune",
        "-from_file=go.mod",
        "-to_macro=go_deps.bzl%go_deps",
    ],
)

go_lint_config(
    name = "go_lint_config",
    exclude_filter = [
        ".connect.go",
        ".gen.go",
        ".pb.go",
        "mock_",
    ],
    visibility = [
        "//visibility:public",
    ],
)

flake8_lint_config(
    name = "flake8_lint_config",
    config_file = ".flake8",
    visibility = [
        "//visibility:public",
    ],
)

# Optional version string to produce versioned file names. End deliverables, such as installable
# packages will have a name derived from that string.
# The flag is to be used when producing publishable assets (so, typically by the CI build).
# The rest of the time the assets will have an unversioned name. The version tags embedded
# in binaries and package manifests are always set. Regardless of this flag.
string_flag(
    name = "file_name_version",
    build_setting_default = "dev",
    visibility = ["//visibility:public"],
)

# Add a build flag to enable bundling the management API documentation with the
# binaries. This can be enabled by passing --//:mgmtapi_bundle_doc=true when
# invoking bazel
bool_flag(
    name = "mgmtapi_bundle_doc",
    build_setting_default = False,
)

config_setting(
    name = "mgmtapi_bundle_doc_build",
    flag_values = {":mgmtapi_bundle_doc": "true"},
)

# This is the SCION distributable package. It contains all (binary) services,
# and the required tools.
pkg_tar(
    name = "scion",
    srcs = [
        "//control/cmd/control",
        "//daemon/cmd/daemon",
        "//dispatcher/cmd/dispatcher",
        "//gateway/cmd/gateway",
        "//router/cmd/router",
        "//scion-pki/cmd/scion-pki",
        "//scion/cmd/scion",
        "//tools/pathdb_dump",
    ],
    mode = "0755",
    package_dir = "",
)

# This contains all of the binaries needed to run CI (integration & acceptance
# tests)
pkg_tar(
    name = "scion-ci",
    srcs = [
        "//acceptance/cmd/sig_ping_acceptance",
        "//acceptance/router_benchmark/brload",
        "//pkg/private/xtest/graphupdater",
        "//tools/braccept",
        "//tools/buildkite/cmd/buildkite_artifacts",
        "//tools/end2end",
        "//tools/end2end_integration",
        "//tools/pktgen/cmd/pktgen",
        "//tools/scion_integration",
        "//tools/udpproxy",
    ],
    mode = "0755",
    package_dir = "",
)

# This contains all of the binaries needed to run the topology generator
# and start a local stack.
pkg_tar(
    name = "scion-topo",
    srcs = [
        "//tools:set_ipv6_addr",
        "//tools:supervisorctl",
        "//tools:supervisord",
        "//tools:topodot",
        "//tools:topogen",
    ],
    mode = "0755",
)

# Nogo - Go code analysis tool
nogo(
    name = "nogo",
    config = "nogo.json",
    visibility = ["//visibility:public"],
    deps = nogo_deps,
)

buildifier(
    name = "buildifier",
)

buildifier(
    name = "buildifier_check",
    mode = "check",
)

spectral_bin.spectral_binary(
    name = "spectral",
)

# Runs all write_source_files targets in this Workspace. To update the list run
# bazel run @com_github_bazelbuild_buildtools//buildozer -- --root_dir $PWD  "add additional_update_targets $( bazel query 'filter("^.*[^\d]$", kind(_write_source_file, //...)) except //:write_all_source_files' | tr '\n' ' ')" //:write_all_source_files
write_source_files(
    name = "write_all_source_files",
    additional_update_targets = [
        "//control/mgmtapi:write_files",
        "//daemon/mgmtapi:write_files",
        "//dispatcher/mgmtapi:write_files",
        "//doc/command:write_files",
        "//gateway/mgmtapi:write_files",
        "//pkg/proto/control_plane/v1/control_planeconnect:write_files",
        "//pkg/proto/daemon/v1/daemonconnect:write_files",
        "//pkg/proto/discovery/v1/discoveryconnect:write_files",
        "//pkg/proto/gateway/v1/gatewayconnect:write_files",
        "//pkg/proto/hidden_segment/v1/hidden_segmentconnect:write_files",
        "//private/ca/api:write_files",
        "//private/mgmtapi/cppki/api:write_files",
        "//private/mgmtapi/health/api:write_files",
        "//private/mgmtapi/segments/api:write_files",
        "//router/mgmtapi:write_files",
        "//spec:write_files",
    ],
)
