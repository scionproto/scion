load("@bazel_skylib//lib:shell.bzl", "shell")

def _openapi_build_docs_impl(ctx):
    cmd = "{bin} build-docs --output {out} {src}".format(
        bin = ctx.executable._openapi_cli.path,
        out = shell.quote(ctx.outputs.out.path),
        src = shell.quote(ctx.file.src.path),
    )

    ctx.actions.run_shell(
        outputs = [ctx.outputs.out],
        inputs = [ctx.file.src],
        tools = [ctx.executable._openapi_cli],
        command = cmd,
        mnemonic = "OpenAPIBuildDocs",
        use_default_shell_env = True,
    )
    return [DefaultInfo(
        files = depset([ctx.outputs.out]),
    )]

openapi_build_docs = rule(
    implementation = _openapi_build_docs_impl,
    doc = "This rule can be used to create API documentation as a single, zero-dependency HTML file.",
    attrs = {
        "src": attr.label(
            doc = "The API definition file",
            allow_single_file = [".yml"],
        ),
        "out": attr.output(
            doc = "The output HTML file generated by this rule",
        ),
        "_openapi_cli": attr.label(
            default = "@rules_openapi_npm//@redocly/cli/bin:openapi",
            executable = True,
            cfg = "target",
        ),
    },
)
