# gazelle:ignore

load("@io_bazel_rules_go//go:def.bzl", "go_library")

go_library(
    name = "go_default_library",
    srcs = [
        "cereal.go",
        "doc.go",
        "sign.go",
        ] + glob(["*.gen.go",
                  "*.capnp.go"]),
    importpath = "github.com/scionproto/scion/go/proto",
    visibility = ["//visibility:public"],
    deps = [
        "//go/lib/common:go_default_library",
        "//go/lib/scrypto:go_default_library",
        "//go/lib/util:go_default_library",
        "@com_zombiezen_go_capnproto2//:go_default_library",
        "@com_zombiezen_go_capnproto2//encoding/text:go_default_library",
        "@com_zombiezen_go_capnproto2//pogs:go_default_library",
        "@com_zombiezen_go_capnproto2//schemas:go_default_library",
    ],
)

# Note: :structs and :capnp are only used to manually regenerate git-controlled
# source files. These targets are no dependencies of other bazel targets.
# Instead, they are built only in `make gogen`.
# To differentiate from the source files used in go_default_library, the files
# are generated into a subdirectory `gogen/`
genrule(
    name = "structs",
    srcs = [],
    outs = ["gogen/structs.gen.go"],
    cmd = "./$(location structs_gen_go.sh) > \"$@\"",
    tools = ["structs_gen_go.sh"],
)

# TODO(sustrik): At the moment we have a copied version of go.capnp in proto directory.
# Once python is Bazelified we can copy it as part of the build process.
# Specifically we would need to add "@com_zombiezen_go_capnproto2//std:go.capnp" to
# srcs and -I$$(dirname \"$(location @com_zombiezen_go_capnproto2//std:go.capnp)\")
# to the command line. Also "go.capnp" inside individual capnp files should be changed
# to "/go.capnp".
genrule(
    name = "capnp",
    srcs = [
        "//proto:sources",
    ],
    outs = [ "gogen/%s" % f for f in [
        "ack.capnp.go",
        "asm_exts.capnp.go",
        "cert_mgmt.capnp.go",
        "common.capnp.go",
        "ctrl_extn.capnp.go",
        "ctrl_pld.capnp.go",
        "drkey_mgmt.capnp.go",
        "if_state.capnp.go",
        "ifid.capnp.go",
        "path_mgmt.capnp.go",
        "path_seg.capnp.go",
        "rev_info.capnp.go",
        "sciond.capnp.go",
        "sibra.capnp.go",
        "sig.capnp.go",
        "sign.capnp.go",
        "svc_resolution.capnp.go",
        "zkid.capnp.go",
    ]],
    cmd = "capnp compile -o$(location @com_zombiezen_go_capnproto2//capnpc-go:capnpc-go):$(@D)/gogen --src-prefix=proto $(locations //proto:sources)",
    tools = ["@com_zombiezen_go_capnproto2//capnpc-go:capnpc-go"],
)
