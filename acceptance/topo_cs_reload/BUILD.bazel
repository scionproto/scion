load("//lint:go.bzl", "go_test")
load("@io_bazel_rules_docker//container:container.bzl", "container_image")

go_test(
    name = "go_default_test",
    srcs = ["reload_test.go"],
    args = [
        "-scion_pki",
        "$(location //go/scion-pki:scion-pki)",
        "-topo",
        "$(location testdata/test.topo)",
        "-crypto_lib",
        "$(location //scripts/cryptoplayground:crypto_lib.sh)",
    ],
    data = [
        "docker-compose.yml",
        "testdata/topology_reload.json",
        ":control.tar",
        ":dispatcher.tar",
        ":invalid_changed_ip",
        ":invalid_changed_port",
        ":testdata/gen_crypto.sh",
        ":testdata/test.topo",
        "//acceptance/topo_common:invalid_reloads",
        "//acceptance/topo_common:topology",
        "//go/scion-pki",
        "//scripts/cryptoplayground:crypto_lib.sh",
    ],
    tags = ["integration"],
    deps = [
        "//go/lib/topology:go_default_library",
        "//go/lib/xtest:go_default_library",
        "@com_github_stretchr_testify//assert:go_default_library",
        "@com_github_stretchr_testify//require:go_default_library",
        "@io_bazel_rules_go//go/tools/bazel:go_default_library",
    ],
)

container_image(
    name = "dispatcher",
    base = "//docker:dispatcher",
    cmd = [
        "--config",
        "/disp.toml",
    ],
    entrypoint = ["/app/dispatcher"],
    files = ["testdata/disp.toml"],
)

container_image(
    name = "control",
    base = "//docker:control",
    cmd = [
        "--config",
        "/cs.toml",
    ],
    entrypoint = ["/app/cs"],
    files = [
        "testdata/cs.toml",
        "testdata/tls.key",
        "testdata/tls.pem",
        "testdata/topology_reload.json",
        ":invalid_changed_ip",
        ":invalid_changed_port",
        "//acceptance/topo_common:invalid_reloads",
        "//acceptance/topo_common:topology",
    ],
)

genrule(
    name = "invalid_changed_ip",
    srcs = ["//acceptance/topo_common:topology"],
    outs = ["topology_invalid_changed_ip.json"],
    cmd = "jq '.control_service[].addr = \"242.42.42.2:31002\"' $(location //acceptance/topo_common:topology) > $@",
)

genrule(
    name = "invalid_changed_port",
    srcs = ["//acceptance/topo_common:topology"],
    outs = ["topology_invalid_changed_port.json"],
    cmd = "jq '.control_service[].addr = \"127.0.0.12:42424\"' $(location //acceptance/topo_common:topology) > $@",
)
