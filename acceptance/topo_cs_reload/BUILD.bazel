load("@io_bazel_rules_docker//container:container.bzl", "container_image")
load("@io_bazel_rules_go//go:def.bzl", "go_test")

go_test(
    name = "go_default_test",
    srcs = ["reload_test.go"],
    data = [
        ":dispatcher",
        ":cs",
        "docker-compose.yml",
        "//acceptance/topo_common:topology",
        "//acceptance/topo_common:invalid_reloads",
        "testdata/topology_reload.json",
        ":invalid_changed_ip",
        ":invalid_changed_port",
    ],
    deps = [
        "//go/lib/topology:go_default_library",
        "@com_github_stretchr_testify//assert:go_default_library",
        "@com_github_stretchr_testify//require:go_default_library",
    ],
)

container_image(
    name = "dispatcher",
    base = "//docker:dispatcher_debug",
    files = ["testdata/disp.toml"],
    entrypoint = [
        "/app/dispatcher",
        "-config",
        "/disp.toml",
    ],
)

container_image(
    name = "cs",
    base = "//docker:cs_debug",
    files = [
        "testdata/cs.toml",
        "//acceptance/topo_common:topology",
        "//acceptance/topo_common:invalid_reloads",
        "testdata/topology_reload.json",
        ":invalid_changed_ip",
        ":invalid_changed_port",
    ],
    tars = [":gen_crypto_tar"],
    entrypoint = [
        "/app/cs",
        "-config",
        "/cs.toml",
    ],
)

genrule(
    name = "invalid_changed_ip",
    srcs = ["//acceptance/topo_common:topology"],
    outs = ["topology_invalid_changed_ip.json"],
    cmd = "jq '.ControlService[].Addrs.IPv4.Public.Addr = \"242.42.42.2\"' $(location //acceptance/topo_common:topology) > $@",
)

genrule(
    name = "invalid_changed_port",
    srcs = ["//acceptance/topo_common:topology"],
    outs = ["topology_invalid_changed_port.json"],
    cmd = "jq '.ControlService[].Addrs.IPv4.Public.L4Port = 42424' $(location //acceptance/topo_common:topology) > $@",
)

genrule(
    name = "gen_crypto_tar",
    srcs = ["testdata/test.topo"],
    outs = [
        "crypto.tar",
    ],
    cmd = "$(location :testdata/gen_crypto.sh) $(location //go/tools/scion-pki:scion-pki) $@ $(location testdata/test.topo)",
    tools = [
        "//go/tools/scion-pki:scion-pki",
        ":testdata/gen_crypto.sh",
    ],
)
