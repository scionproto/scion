load("//tools/lint:go.bzl", "go_library", "go_test")

# If you want to read the generated file:
# bazel-bin/router/underlayproviders/af_packet_udpip/bpf_filter_bpfel.go
genrule(
    name = "gen_bpf_filter_go",
    srcs = [
        "portfilter.c",
        "bpf_helpers.h",
        "bpf_helper_defs.h",
    ],
    outs = [
        "portfilter_bpfel.go",
        "portfilter_bpfel.o",
    ],
    cmd = "GOPACKAGE=afpacketudpip $(execpath @com_github_cilium_ebpf//cmd/bpf2go) -output-dir $$(dirname $(location portfilter_bpfel.go)) -tags linux portfilter $(location portfilter.c)",
    tools = ["@com_github_cilium_ebpf//cmd/bpf2go"],
)

go_library(
    name = "go_default_library",
    srcs = [
        "bpf_helper_defs.h",
        "bpf_helpers.h",
        "portfilter_bpfel.go",
        "portfilter_lint.go",  # keep
        "rawsocket.go",
    ],
    embedsrcs = [
        "portfilter_bpfel.o",  #keep
    ],
    importpath = "github.com/scionproto/scion/router/underlayproviders/afpacketudpip",
    visibility = ["//visibility:public"],
    deps = [
        "@com_github_cilium_ebpf//:go_default_library",
        "@org_golang_x_sys//unix:go_default_library",
    ],
)

# Builds the rawsock test; but it will be missing the required capabilities; so we must not
# attempt to run it. We are forced to name the artefact "go_default_test"; otherwise gazelle
# creates its own incorrect version.
go_test(
    name = "go_default_test",
    srcs = ["rawsocket_test.go"],
    embed = [":go_default_library"],
    tags = ["manual"],
    deps = [
        "@com_github_stretchr_testify//assert:go_default_library",
    ],
)

# Adds required capabilities to the rawsock test. Otherwise it cannot be executed.
genrule(
    name = "gen_rawsock_test_cap",
    testonly = True,
    srcs = [
        "go_default_test",
    ],
    outs = [
        "rawsock_test_cap",
    ],
    cmd_bash = "cp $< $@ && /usr/bin/sudo setcap \"cap_bpf+ep cap_net_admin+ep cap_net_raw+ep\" $@",
)

# This is the rule that actually runs the rawsock test.
sh_test(
    name = "rawsock_test",
    testonly = True,
    srcs = ["rawsock_test_cap"],
    tags = ["unit"],
)
